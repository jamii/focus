pub const util = @import("./focus/util.zig");
pub const config = @import("focus_config");
pub const Atlas = @import("./focus/atlas.zig").Atlas;
pub const Buffer = @import("./focus/buffer.zig").Buffer;
pub const LineWrappedBuffer = @import("./focus/line_wrapped_buffer.zig").LineWrappedBuffer;
pub const Editor = @import("./focus/editor.zig").Editor;
pub const SingleLineEditor = @import("./focus/single_line_editor.zig").SingleLineEditor;
pub const Selector = @import("./focus/selector.zig").Selector;
pub const FileOpener = @import("./focus/file_opener.zig").FileOpener;
pub const ProjectFileOpener = @import("./focus/project_file_opener.zig").ProjectFileOpener;
pub const BufferOpener = @import("./focus/buffer_opener.zig").BufferOpener;
pub const BufferSearcher = @import("./focus/buffer_searcher.zig").BufferSearcher;
pub const ProjectSearcher = @import("./focus/project_searcher.zig").ProjectSearcher;
pub const Launcher = @import("./focus/launcher.zig").Launcher;
pub const Maker = @import("./focus/maker.zig").Maker;
pub const ErrorLister = @import("./focus/error_lister.zig").ErrorLister;
pub const Window = @import("./focus/window.zig").Window;
pub const Language = @import("./focus/language.zig").Language;
pub const ChildProcess = @import("./focus/child_process.zig").ChildProcess;
pub const style = @import("./focus/style.zig");
pub const mach_compat = @import("./focus/mach_compat.zig");

const std = @import("std");
const u = util;
const c = util.c;

pub const Request = union(enum) {
    CreateEmptyWindow,
    CreateLauncherWindow,
    CreateEditorWindow: []const u8,
};

// TODO this should just be std.net.Address, but it calculates the wrong address length for abstract domain sockets
pub const Address = struct {
    address: std.net.Address,
    address_len: std.posix.socklen_t,
};

pub const RequestAndClientAddress = struct {
    request: Request,
    client_address: Address,
};

pub const ServerSocket = struct {
    address: Address,
    socket: std.posix.socket_t,
    state: State,

    const State = enum {
        Bound,
        Unbound,
    };
};

pub fn daemonize(log_filename: []const u8) enum { Parent, Child } {
    // https://stackoverflow.com/questions/17954432/creating-a-daemon-in-linux/17955149#17955149
    if (std.posix.fork()) |pid| {
        if (pid != 0) return .Parent;
    } else |err| {
        u.panic("Failed to fork: {}", .{err});
    }
    {
        const err = c.setsid();
        if (err < 0) u.panic("Failed to setsid: {}", .{err});
    }
    // TODO
    //c.signal(c.SIGCHLD, std.posix.linux.SIG_IGN);
    //c.signal(c.SIGHUP, std.posix.linux.SIG_IGN);
    if (std.posix.fork()) |pid| {
        if (pid != 0) std.posix.exit(0);
    } else |err| {
        u.panic("Failed to fork: {}", .{err});
    }
    std.posix.chdir(@ptrCast(config.home_path)) catch
        u.panic("Failed to chdir", .{});

    // redirect stdout/err to log
    const log = std.fs.cwd().createFile(log_filename, .{ .read = false, .truncate = false }) catch |err| u.panic("Failed to open log file ({s}): {}", .{ log_filename, err });
    log.seekFromEnd(0) catch |err| u.panic("Failed to seek to end of log file: {}", .{err});
    _ = std.posix.dup2(log.handle, std.io.getStdOut().handle) catch |err| u.panic("Failed to redirect stdout to log: {}", .{err});
    _ = std.posix.dup2(log.handle, std.io.getStdErr().handle) catch |err| u.panic("Failed to redirect stderr to log: {}", .{err});
    std.debug.print("\n\nFocus daemon started at {}\n", .{std.time.timestamp()});

    return .Child;
}

pub fn createServerSocket(socket_path: []const u8) ServerSocket {
    var address = std.net.Address.initUnix(socket_path) catch |err| u.panic("Failed to init unix socket address: {}", .{err});
    // have to get len before setting the null byte or returns wrong address
    const address_len = address.getOsSockLen();
    address.un.path[0] = 0;
    const socket = std.posix.socket(std.posix.AF.UNIX, std.posix.SOCK.DGRAM | std.posix.SOCK.CLOEXEC, 0) catch |err| u.panic("Failed to create unix socket: {}", .{err});
    const state: ServerSocket.State = if (std.posix.bind(socket, &address.any, address_len))
        .Bound
    else |err| switch (err) {
        error.AddressInUse => .Unbound,
        else => u.panic("Failed to connect to unix socket: {}", .{err}),
    };
    return ServerSocket{
        .address = .{
            .address = address,
            .address_len = address_len,
        },
        .socket = socket,
        .state = state,
    };
}

pub const ClientSocket = struct {
    socket: std.posix.socket_t,
};

pub fn createClientSocket() std.posix.socket_t {
    // with zero-length name, will get an autogenerated name
    var address = std.net.Address.initUnix("") catch |err| u.panic("Failed to init unix socket address: {}", .{err});
    // TODO address.getOsSockLen() returns the wrong length for autogenerated names
    const address_len = @sizeOf(c_short);
    address.un.path[0] = 0;
    const socket = std.posix.socket(std.posix.AF.UNIX, std.posix.SOCK.DGRAM, 0) catch |err| u.panic("Failed to create unix socket: {}", .{err});
    std.posix.bind(socket, &address.any, address_len) catch |err| u.panic("Failed to connect to unix socket: {}", .{err});
    return socket;
}

pub fn sendRequest(client_socket: std.posix.socket_t, server_socket: ServerSocket, request: Request) void {
    const message = switch (request) {
        .CreateEmptyWindow => "CreateEmptyWindow",
        .CreateLauncherWindow => "CreateLauncherWindow",
        .CreateEditorWindow => |filename| filename,
    };
    const len = std.posix.sendto(client_socket, message, 0, &server_socket.address.address.any, server_socket.address.address_len) catch |err| u.panic("Failed to send request: {}", .{err});
    u.assert(len == message.len);
}

pub fn receiveRequest(buffer: []u8, server_socket: ServerSocket) ?RequestAndClientAddress {
    var client_address: std.posix.sockaddr = undefined;
    // TODO have no idea if this is the correct value
    var client_address_len: std.posix.socklen_t = @sizeOf(std.posix.sockaddr);
    const len = std.posix.recvfrom(server_socket.socket, buffer, std.posix.MSG.DONTWAIT, &client_address, &client_address_len) catch |err| {
        switch (err) {
            error.WouldBlock => return null,
            else => u.panic("Failed to recv request: {}", .{err}),
        }
    };
    const message = buffer[0..len];
    // TODO Relying on anon->Request coercion here breaks - in ReleaseSafe we get .{.CreateEditorWindow = "CreateEmptyWindow"}
    const request = if (std.mem.eql(u8, message, "CreateEmptyWindow"))
        Request{ .CreateEmptyWindow = {} }
    else if (std.mem.eql(u8, message, "CreateLauncherWindow"))
        Request{ .CreateLauncherWindow = {} }
    else
        Request{ .CreateEditorWindow = message };
    return RequestAndClientAddress{
        .request = request,
        .client_address = .{
            .address = .{ .any = client_address },
            .address_len = client_address_len,
        },
    };
}

pub fn sendReply(server_socket: ServerSocket, client_address: Address, exit_code: u8) void {
    _ = std.posix.sendto(server_socket.socket, &[1]u8{exit_code}, 0, &client_address.address.any, client_address.address_len) catch |err| u.warn("Failed to send reply: {}\n", .{err});
}

// returns exit code
pub fn waitReply(client_socket: std.posix.socket_t) u8 {
    var buffer: [1]u8 = undefined;
    const len = std.posix.recv(client_socket, &buffer, 0) catch |err| {
        u.panic("Failed to recv reply: {}", .{err});
    };
    u.assert(len == 1);
    return buffer[0];
}

const ns_per_frame = @divTrunc(1_000_000_000, 60);

pub fn run(allocator: u.Allocator, server_socket: ServerSocket) void {
    var app = App.init(allocator, server_socket);
    var timer = std.time.Timer.start() catch u.panic("Couldn't start timer", .{});
    while (true) {
        _ = timer.lap();
        app.frame();
        const used_ns = timer.read();
        if (used_ns < ns_per_frame)
            // TODO can we correct for drift from sleep imprecision?
            std.time.sleep(ns_per_frame - used_ns)
        else
            u.warn("Frame took {} ns\n", .{used_ns});
    }
}

pub const App = struct {
    allocator: u.Allocator,
    server_socket: ServerSocket,
    frame_arena: u.ArenaAllocator,
    frame_allocator: u.Allocator,
    atlas: *Atlas,
    // contains only buffers that were created from files
    // other buffers are just floating around but must be deinited by their owning view
    buffers: u.DeepHashMap([]const u8, *Buffer),
    windows: u.ArrayList(*Window),
    frame_time_ms: i64,
    // used for both buffer_searcher and project_searcher
    last_search_filter: []const u8,
    last_project_search_selected: usize,
    last_file_filter: []const u8,
    last_project_file_opener_selected: usize,
    last_buffer_opener_selected: usize,
    last_error_lister_selected: usize,

    fn glfw_error_callback(error_code: c_int, description: [*c]const u8) callconv(.C) void {
        std.debug.print("{}: {s}\n", .{ error_code, description });
    }

    pub fn init(allocator: u.Allocator, server_socket: ServerSocket) *App {
        _ = c.glfwSetErrorCallback(glfw_error_callback);
        if (c.glfwInit() != 0)
            u.panic("Error starting glfw", .{});

        const atlas = allocator.create(Atlas) catch u.oom();
        atlas.* = Atlas.init(allocator, 16);
        var self = allocator.create(App) catch u.oom();
        self.* = App{
            .allocator = allocator,
            .server_socket = server_socket,
            .frame_arena = u.ArenaAllocator.init(allocator),
            .frame_allocator = undefined,
            .atlas = atlas,
            .buffers = u.DeepHashMap([]const u8, *Buffer).init(allocator),
            .windows = u.ArrayList(*Window).init(allocator),
            .frame_time_ms = 0,
            .last_search_filter = "",
            .last_project_search_selected = 0,
            .last_file_filter = "",
            .last_project_file_opener_selected = 0,
            .last_buffer_opener_selected = 0,
            .last_error_lister_selected = 0,
        };
        self.frame_allocator = self.frame_arena.allocator();

        return self;
    }

    pub fn deinit(self: *App) void {
        self.allocator.free(self.last_file_filter);
        self.allocator.free(self.last_search_filter);

        for (self.windows.items) |window| {
            window.deinit();
            self.allocator.destroy(window);
        }
        self.windows.deinit();

        var buffer_iter = self.buffers.iterator();
        while (buffer_iter.next()) |entry| {
            self.allocator.free(entry.key_ptr.*);
            entry.value_ptr.deinit();
        }
        self.buffers.deinit();

        self.atlas.deinit();
        self.allocator.destroy(self.atlas);

        self.frame_arena.deinit();

        self.allocator.destroy(self);

        if (u.builtin.mode == .Debug) {
            _ = @import("root").gpa.detectLeaks();
        }

        c.glfwTerminate();
    }

    pub fn quit(self: *App) noreturn {
        self.deinit();
        std.posix.exit(0);
    }

    pub fn getBufferFromAbsoluteFilename(self: *App, absolute_filename: []const u8) *Buffer {
        if (self.buffers.get(absolute_filename)) |buffer| {
            return buffer;
        } else {
            const buffer = Buffer.initFromAbsoluteFilename(self, .{}, absolute_filename);
            // buffer might be out of date if file was modified elsewhere
            buffer.refresh();
            self.buffers.put(self.dupe(absolute_filename), buffer) catch u.oom();
            return buffer;
        }
    }

    pub fn registerWindow(self: *App, window: Window) *Window {
        const window_ptr = self.allocator.create(Window) catch u.oom();
        window_ptr.* = window;
        self.windows.append(window_ptr) catch u.oom();
        return window_ptr;
    }

    pub fn deregisterWindow(self: *App, window: *Window) void {
        const i = std.mem.indexOfScalar(*Window, self.windows.items, window).?;
        _ = self.windows.swapRemove(i);
        self.allocator.destroy(window);
    }

    pub fn handleRequest(self: *App, request: Request, client_address: Address) void {
        var new_window: *Window = undefined;
        switch (request) {
            .CreateEmptyWindow => {
                new_window = self.registerWindow(Window.init(self, .NotFloating));
                new_window.client_address_o = client_address;
            },
            .CreateLauncherWindow => {
                new_window = self.registerWindow(Window.init(self, .Floating));
                new_window.client_address_o = client_address;
                const launcher = Launcher.init(self);
                new_window.pushView(launcher);
            },
            .CreateEditorWindow => |filename| {
                new_window = self.registerWindow(Window.init(self, .NotFloating));
                new_window.client_address_o = client_address;
                const new_buffer = self.getBufferFromAbsoluteFilename(filename);
                const new_editor = Editor.init(self, new_buffer, .{});
                new_window.pushView(new_editor);
            },
        }
    }

    pub fn frame(self: *App) void {
        self.frame_time_ms = std.time.milliTimestamp();

        // reset arena
        self.frame_arena.deinit();
        self.frame_arena = u.ArenaAllocator.init(self.allocator);

        // check for requests
        const buffer = self.frame_allocator.alloc(u8, 256 * 1024) catch u.oom();
        while (receiveRequest(buffer, self.server_socket)) |request_and_client_address| {
            self.handleRequest(request_and_client_address.request, request_and_client_address.client_address);
        }

        // refresh visible buffers
        for (self.windows.items) |window| {
            if (window.getTopViewIfEditor()) |editor|
                editor.buffer.refresh();
        }

        // poll events
        // TODO use waitEventsTimeout to handle fps
        c.glfwPollEvents();

        // run window frames
        // copy window list because it might change during frame
        const current_windows = self.frame_allocator.dupe(*Window, self.windows.items) catch u.oom();
        for (current_windows) |window| window.frame();
    }

    pub fn dupe(self: *App, slice: anytype) @TypeOf(slice) {
        return self.allocator.dupe(@typeInfo(@TypeOf(slice)).pointer.child, slice) catch u.oom();
    }

    pub fn changeFontSize(self: *App, increment: isize) void {
        self.atlas.deinit();
        const new_char_size_pixels = @as(isize, @intCast(self.atlas.char_size_pixels)) + increment;
        if (new_char_size_pixels >= 0) {
            self.atlas.* = Atlas.init(self.allocator, @intCast(new_char_size_pixels));
            for (self.windows.items) |window| {
                window.loadAtlasTexture(self.atlas);
            }
        }
    }

    pub fn getCompletions(self: *App, language: std.meta.Tag(Language), prefix: []const u8) [][]const u8 {
        var results = u.ArrayList([]const u8).init(self.frame_allocator);

        var buffer_iter = self.buffers.iterator();
        while (buffer_iter.next()) |entry| {
            if (std.meta.activeTag(entry.value_ptr.*.language) == language)
                entry.value_ptr.*.getCompletionsInto(prefix, &results);
        }

        std.mem.sort([]const u8, results.items, {}, struct {
            fn lessThan(_: void, a: []const u8, b: []const u8) bool {
                return std.mem.lessThan(u8, a, b);
            }
        }.lessThan);

        var unique_results = u.ArrayList([]const u8).init(self.frame_allocator);
        for (results.items, 0..) |result, i| {
            if (i == 0 or !std.mem.eql(u8, result, results.items[i - 1]))
                unique_results.append(result) catch u.oom();
        }

        return unique_results.toOwnedSlice() catch u.oom();
    }

    pub fn handleAfterSave(self: *App) void {
        for (self.windows.items) |window| window.handleAfterSave();
    }
};
